# Buscando cart贸grafos

Usamos el editor SPARQL de Wikipedia https://query.wikidata.org/

https://wdqs-tutorial.toolforge.org/
https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples
https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples/advanced
https://www.wikidata.org/wiki/Wikidata:Request_a_query
https://js.histropedia.com/apps/query-timeline/
https://en.wikibooks.org/wiki/SPARQL
https://davidsbatista.net/blog/2023/01/19/SPARQL_WikiData/

Nuestro resultset van a ser personas, que vamos a escoger por su ocupaci贸n. Esto hace que el atributo que nos interesa para filtrar es `wdt:P106`. En Wikidata no todas las enidades tienen todos sus atributos rellenos. Hay catalogaciones incompletas, bien por errores o por desconocimiento, que hacen que a muchas entidades les falten ciertos atributos. Por eso al hacer nuestra consulta SPARQL seleccionamos registros que tienen este atributo relleno, para poder filtrar por el.

* Utilizamos el servicio **wikibase:label** para poder convertir entidades Wikidata en texto legible.
* `?person` hace referencia a la entidad y `?personLabel` a la entidad legible.
* Al comienxo es mejor limirtar siempre los resultados, para evitar colapsar los servicios
* Establecemos cl谩usula restrictiva, que obligue a que los registros listados tengan el atributo `wdt:P106` relleno
* A este atributo, lo llamamos `?empleo`, que es el nombre que ponemos en el **SELECT**.
* Despu茅s ponemos un filtro, `person wdt:P106 wd:Q1734662`, en donde establecemos que uno de sus atributo empleo `wdt:P106` debe ser cart贸grafo `wd:Q1734662`

La cl谩usula de consulta resultante es 

```sql
SELECT ?person ?personLabel ?empleo ?empleoLabel 
  WHERE {
    # Aqu铆 ponemos las restrictivas. Para que salga registro, este campo debe estar relleno
    ?person wdt:P106 ?empleo.
    
    # FILTROS
    # Filtramos personas cuya ocupaci贸n (P106) es cart贸grafo(wd:Q1734662)
    ?person wdt:P106 wd:Q1734662
    
    SERVICE wikibase:label { 
      bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en"
    }
  }
 limit 10
```

Y los datos obtenidos

![](img/01result.png)

Si no establecemos orden, los resultados se ordenan por relevancia, en este caso del personaje. **George Washington** fue un cart贸grafo coco conocido, pero se lista el primero porque su relevancia por sus otras ocupaciones es enorme. Aparace varias veces porque se listan una a una todas sus ocupaciones asignadas.
Vemos que `?person` y `?empleo` muestran las entidades Wikidata. Pinchando en ella accedemos al elemento resultante. Pero para que aparezcan en los resultados, hacemos uso del servicio **wikibase:label**, que nos permite enlazar las entidades con sus denominaciones. Las denominacione pueden cambiar seg煤n el idioma, as铆 que podemos definir el idioma de retorno o dejar que el servicio lo estableca por nosotros

```js
// As铆  lo establece por defecto, poniendo si primero el termino espa帽ol y depu茅s el ingl茅s
bd:serviceParam wikibase:language "[AUTO_LANGUAGE],es,en". 
// De esta manera  devolver铆a los textos Label en alem谩n
bd:serviceParam wikibase:language "de". 
```

Vamos a poner los dato un poco mejpor, agrupando el empleo en una l铆nea: de esta forma utilizamos una funci贸n de agregaci贸n **group_concat** para concatenar los valores, y el resultado obtenido es mucho m谩s llevadero.

```sql
SELECT ?person ?personLabel (group_concat(?empleoLabel;separator=",") as ?lstempleos)
  WHERE {
    # Aqu铆 ponemos las restrictivas. Para que salga registro, el campo debe estar relleno
    ?person wdt:P106 ?empleo.
    
    # FILTROS
    # Filtramos personas cuya ocupaci贸n (P106) es cart贸grafo(wd:Q1734662)
    ?person wdt:P106 wd:Q1734662
    
    SERVICE wikibase:label { 
      bd:serviceParam wikibase:language "[AUTO_LANGUAGE],es,en".
      ?empleo rdfs:label ?empleoLabel .
      ?person rdfs:label ?personLabel .
    }
  }
group by ?person ?personLabel
limit 10
```

![](img/02result.png)

Ahora los resultados nos muestran todas las profesiones de los personajes en una l铆nea. Y seguimos ordenando por relevancia. La lista cuadra.

En esta investigaci贸n nos puede ser 煤til que si hay foto, que la muestre, pero que si no la hay, no omita el registro. Por tanto, queremos que siempre que sea posible aparezca la foto. Para ello introducimos la cl谩usula `OPTIONAL{ ?person wdt:P18 ?foto }`

```sql
SELECT ?person ?personLabel (group_concat(?empleoLabel;separator=",") as ?lstempleos) ?foto
  WHERE {
    # Aqu铆 ponemos las restrictivas. Para que salga registro, el campo debe estar relleno
    ?person wdt:P106 ?empleo.
    
    # FILTROS
    # Filtramos personas cuya ocupaci贸n (P106) es cart贸grafo(wd:Q1734662)
    ?person wdt:P106 wd:Q1734662
            
    # Aqu铆 ponemos las opcionales (puede haber valores de ese campo o no)
    # Queremos que salgan los registros que tengan campo imagen. Adem谩s hacemos un alias ?foto y lo ponemos en el select 
    OPTIONAL{ ?person wdt:P18 ?foto }
      
    SERVICE wikibase:label { 
      bd:serviceParam wikibase:language "[AUTO_LANGUAGE],es,en".
      ?empleo rdfs:label ?empleoLabel .
      ?person rdfs:label ?personLabel .
    }
  }
group by ?person ?personLabel ?foto
limit 10
```

Podemos ver en la vista **Image Grid** que el resultado obtenido encuentra fotos para 9 de los 10 personajes.

![](img/03result.png)

Sin embargo, si volvemos al modo tabla, si lista los 10, uno de ellos sin imagen, Arno Peters.

![](img/04result.png)

Ahora vamos a filtrar un poco m谩s. No s贸lo nos valen los cart贸grafos, tambi茅n los ge贸grafos, as铆 que podemos a帽adir una segunda profesi贸n a nuestro filtro.

```sql
SELECT ?person ?personLabel (group_concat(?empleoLabel;separator=",") as ?lstempleos) ?foto
  WHERE {
    # Aqu铆 ponemos las restrictivas. Para que salga registro, el campo debe estar relleno
    ?person wdt:P106 ?empleo.
    
    # FILTROS
    # Filtramos personas cuya ocupaci贸n (P106) es cart贸grafo(wd:Q1734662)
    #?person wdt:P106 wd:Q1734662
    # Filtramos personas cuya ocupaci贸n (P106) es cart贸grafo(wd:Q1734662) y ge贸grafo (wd:Q901402), ambas incluidas (usamos ;) (puede tener m谩s)
    ?person wdt:P106 wd:Q1734662;wdt:P106 wd:Q901402


    # Aqu铆 ponemos las opcionales (puede haber valores de ese campo o no)
    OPTIONAL{ ?person wdt:P18 ?foto }  #Queremos que salgan los registros con campo imagen. Y hacemos un alias ?foto y lo ponemos en el select
      
    
    SERVICE wikibase:label { 
      bd:serviceParam wikibase:language "[AUTO_LANGUAGE],es,en".
      ?empleo rdfs:label ?empleoLabel .
      ?person rdfs:label ?personLabel .
    }
  }
group by ?person ?personLabel ?foto
limit 10
```

Ahora en los resultados vemos que desaparece, entre otros, Washington, porque en Wikidata era cart贸grafo, pero no ge贸grafo.

* Sentencias equivalentes, que buscan las ocupaciones cart贸grafo(wd:Q1734662) y ge贸grafo (wd:Q901402). Ambas deben estar presentes
  * `#?person wdt:P106 wd:Q901402,wd:Q1734662.`
  * `#?person wdt:P106 wd:Q1734662,wd:Q901402.`
  * `?person wdt:P106 wd:Q1734662,wd:Q901402.`
* Si queremos que los personajes tengan asigndas las dos propiedades, o s贸lo una de ellas, podemos usar la opci贸n filter
  * `FILTER ( ?empleo in ( wd:Q901402,wd:Q1734662 ) )`


Por 煤ltimo, podemos extender la consulta para sacar los cart贸grafos y/o ge贸grafos que nacieron o murieron en un determinado periodo, para sacar efem茅rides

```sql
SELECT ?person ?personLabel (group_concat(?empleoLabel;separator=",") as ?lstempleos) ?birthDate ?birthPlace ?deathDate ?deathPlace ?foto
  WHERE {
    # Aqu铆 ponemos las restrictivas. Para que salga registro, el campo debe estar relleno
    ?person wdt:P106 ?empleo.
    ?person wdt:P569 ?birthDate.
    ?person wdt:P19 ?birthPlace.  # Con esto sacamos el Lugar de Nacimiento, pero no vienen coordenadas
    ?person wdt:P570 ?deathDate.  # Con  esto sacamos la fecha de la muerte
    ?person wdt:P20 ?deathPlace.  # Con esto sacamos el Lugar de fallecimiento

    OPTIONAL{ ?person wdt:P18 ?foto }  
       
    # Aqu铆 ponemos las opcionales (puede haber valores de ese campo o no)
    FILTER ( ?empleo in ( wd:Q901402,wd:Q1734662 ) )

    # Filtramos personas nacidas o fallecidas entre el 17 y el 21 de junio
    FILTER(
     (month(?birthDate) = 6 && day(?birthDate) >= 17) && (month(?birthDate) = 6 && day(?birthDate) <= 21) ||
     (month(?deathDate) = 6 && day(?deathDate) >= 17) && (month(?deathDate) = 6 && day(?deathDate) <= 21)
    )

    
    SERVICE wikibase:label { 
      bd:serviceParam wikibase:language "[AUTO_LANGUAGE],es,en".
      ?empleo rdfs:label ?empleoLabel .
      ?person rdfs:label ?personLabel .
    }
  }
group by ?person ?personLabel ?birthDate ?birthPlace ?deathDate ?deathPlace ?foto
limit 100

```

Vemos que salen m谩s resultados de los que deber铆an salir, porque muchis personajes tienen asociadas varias fotos, y por cada una de ellas se repite el registro. Si no tenemos predilecci贸n por ninguna de ellas, podemos usar la funci贸n de agregaci贸n **SAMPLE** para que agrupe y seleccione al azar una de ellas. Si a eto ordenamos por nombre

```sql
SELECT ?person ?personLabel (group_concat(?empleoLabel;separator=",") as ?lstempleos) ?birthDate ?birthPlaceLabel ?deathDate ?deathPlaceLabel (SAMPLE(?foto) AS ?single)
  WHERE {
    # Aqu铆 ponemos las restrictivas. Para que salga registro, el campo debe estar relleno
    ?person wdt:P106 ?empleo.
    ?person wdt:P569 ?birthDate.
    ?person wdt:P19 ?birthPlace.  # Con esto sacamos el Lugar de Nacimiento, pero no vienen coordenadas
    ?person wdt:P570 ?deathDate.  # Con  esto sacamos la fecha de la muerte
    ?person wdt:P20 ?deathPlace.  # Con esto sacamos el Lugar de fallecimiento

    OPTIONAL{ ?person wdt:P18 ?foto }  #Queremos que salgan los registros que tengan campo imagen. Adem谩s hacemos un alias ?foto y lo ponemos en el select para que los liste
    
    # FILTROS
    # Filtramos personas cuya ocupaci贸n (P106) es surveyor o agrimensor(wd:Q294126)
    #?person wdt:P106 wd:Q1734662    
    # Filtramos personas cuya ocupaci贸n (P106) es cart贸grafo(wd:Q1734662)
    #?person wdt:P106 wd:Q1734662
    # Filtramos personas cuya ocupaci贸n (P106) es cart贸grafo(wd:Q1734662) y ge贸grafo (wd:Q901402), ambas incluidas (usamos ;) (puede tener m谩s)
    #?person wdt:P106 wd:Q901402;wdt:P106 wd:Q1734662.
    #?person wdt:P106 wd:Q901402,wd:Q1734662.
    #?person wdt:P106 wd:Q1734662,wd:Q901402.
    
    # Aqu铆 ponemos las opcionales (puede haber valores de ese campo o no)
    
    
    FILTER ( ?empleo in ( wd:Q901402,wd:Q1734662,wd:Q294126 ) )

      # Filtramos personas nacidas o fallecidas entre el 17 y el 21 de junio
    FILTER(
     (month(?birthDate) = 6 && day(?birthDate) >= 17) && (month(?birthDate) = 6 && day(?birthDate) <= 21) ||
     (month(?deathDate) = 6 && day(?deathDate) >= 17) && (month(?deathDate) = 6 && day(?deathDate) <= 21)
    )

    # Como hacemos una funci贸n de agregaci贸n, la asociaci贸n con el Label de los campos se establece aqu铆.
    SERVICE wikibase:label { 
      bd:serviceParam wikibase:language "[AUTO_LANGUAGE],es,en".
      ?empleo rdfs:label ?empleoLabel .
      ?person rdfs:label ?personLabel .
      ?birthPlace rdfs:label ?birthPlaceLabel .
      ?deathPlace rdfs:label ?deathPlaceLabel .
    }
  }
group by ?person ?personLabel ?birthDate ?birthPlaceLabel ?deathDate ?deathPlaceLabel 
order by ?personLabel
limit 100
```

http://www.ign.es/web/biblioteca_cartoteca/abnetcl.cgi?TITN=63667